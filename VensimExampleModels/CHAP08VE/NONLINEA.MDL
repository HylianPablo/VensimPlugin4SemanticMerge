********************************************************
	.NonlinearPopulation
********************************************************~
		
		Nonlinear Population Growth Model
		John Sterman (1999) Business Dynamics.  Irwin/McGraw-Hill
		Copyright (c) 1999 John Sterman
		
		This is the generic nonlinear, first-order population growth
		model.  The fractional birth and death rates depend on the ratio 
		of the population to the carrying capacity.
		Chapter 8.
	|

Initial Population=
	2
	~	People
	~	The initial population.
	|

Birth Rate = Fractional Birth Rate*Population
	~	People/Year
	~	Births are proportional to the population.
	|

Carrying Capacity=
	 1000
	~	People
	~	The carrying capacity defines the equilibrium or maximum sustainable \
		population.
	|

Death Rate = Fractional Death Rate*Population
	~	People/Year
	~	Deaths are proportional to the population.
	|

Fractional Birth Rate = 1-(1/(1+exp(-7*(Population Relative to Carrying Capacity-1)))\
		)
	~	1/Year
	~	The fractional birth rate is a declining function of the population \
		relative to the carrying capacity.  A logistic function is used.
	|

Fractional Death Rate = 0.25+0.25*Population Relative to Carrying Capacity^4
	~	1/Year
	~	The fractional death rate is an increasing function of the ratio of \
		population to carrying capacity.  A power function is assumed.
	|

Net Birth Rate = Birth Rate-Death Rate
	~	People/Year
	~	The net birth rate is births less deaths.
	|

Net Fractional Birth Rate = Fractional Birth Rate-Fractional Death Rate
	~	1/Year
	~	The net fractional birth rate is fractional births less fractional deaths.
	|

Population= INTEG (
	Birth Rate-Death Rate,
		Initial Population)
	~	People
	~	The population is increased by births and decreased by deaths.
	|

Population Relative to Carrying Capacity=
	 Population/Carrying Capacity
	~	Dimensionless
	~	The ratio of population to carrying capacity determines the fractional \
		birth and death rates
	|

********************************************************
	.Control
********************************************************~
		Simulation Control Paramaters
	|

FINAL TIME  = 12
	~	Year
	~	The final time for the simulation.
	|

INITIAL TIME  = 0
	~	Year
	~	The initial time for the simulation.
	|

SAVEPER  = 
        TIME STEP
	~	Year
	~	The frequency with which output is stored.
	|

TIME STEP  = 0.125
	~	Year
	~	The time step for the simulation.
	|

\\\---/// Sketch information - do not modify anything except names
V300  Do not put anything below this section - it will be ignored
*View 1
$0,0,Helvetica|10|B|0-0-0|0-0-0|0-0-0|-1--1--1|-1--1--1|96,96
10,1,Population,313,60,40,20,3,3,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
12,2,48,121,59,8,8,0,3,0,50,-1,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
11,3,48,188,59,6,8,34,3,0,0,1,0,0,0
10,4,Birth Rate,188,87,40,20,40,3,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
12,5,48,501,60,8,8,0,3,0,50,-1,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
11,6,48,422,61,6,8,34,3,0,0,1,0,0,0
10,7,Death Rate,422,86,28,17,40,3,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
10,8,Population Relative to Carrying Capacity,319,203,49,31,8,3,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
10,9,Carrying Capacity,312,283,40,20,8,3,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|14|B|0-0-0
12,10,0,360,99,14,14,5,4,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|12|B|0-0-0
B
12,11,0,384,168,14,14,5,4,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|12|B|0-0-0
B
12,12,0,253,167,14,14,4,4,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|12|B|0-0-0
B
12,13,0,262,98,14,14,4,4,0,50,0,0,0,0,-1--1--1,0-0-0,Helvetica|12|B|0-0-0
R
10,14,Fractional Birth Rate,164,194,40,20,8,3,0,0,0,0,0,0
10,15,Fractional Death Rate,452,194,49,20,8,3,0,0,0,0,0,0
10,16,Net Birth Rate,545,405,40,20,8,3,0,0,0,0,0,0
10,17,Birth Rate,416,378,40,20,8,2,0,3,0,0,0,0,128-128-128,0-0-0,|10|B|128-128-128
10,18,Death Rate,417,444,40,20,8,2,0,3,0,0,0,0,128-128-128,0-0-0,|10|B|128-128-128
10,19,Net Fractional Birth Rate,130,408,40,20,8,3,0,0,0,0,0,0
10,20,Fractional Birth Rate,270,377,49,21,8,2,0,3,0,0,0,0,128-128-128,0-0-0,|10|B|128-128-128
10,21,Fractional Death Rate,265,444,51,17,8,2,0,3,0,0,0,0,128-128-128,0-0-0,|10|B|128-128-128
1,22,3,1,4,0,0,22,0,0,0,-1--1--1,,1|(233,59)|
1,23,3,2,100,0,0,22,0,0,0,-1--1--1,,1|(155,59)|
1,24,6,5,4,0,0,22,0,0,0,-1--1--1,,1|(460,61)|
1,25,6,1,100,0,0,22,0,0,0,-1--1--1,,1|(384,61)|
1,26,1,8,1,0,43,0,51,0,0,0-0-0,Helvetica|14|B|0-0-0,1|(313,168)|
1,27,1,7,2,0,43,0,51,0,0,0-0-0,Helvetica|14|B|0-0-0,1|(403,116)|
1,28,1,4,2,0,43,0,51,0,0,0-0-0,Helvetica|14|B|0-0-0,1|(212,121)|
1,29,9,8,2,0,45,0,51,0,0,0-0-0,Helvetica|14|B|0-0-0,1|(324,237)|
1,30,8,14,2,0,45,0,0,0,0,-1--1--1,,1|(187,225)|
1,31,14,4,2,0,43,0,0,0,0,-1--1--1,,1|(147,115)|
1,32,8,15,2,0,43,0,0,0,0,-1--1--1,,1|(433,226)|
1,33,15,7,2,0,43,0,0,0,0,-1--1--1,,1|(463,94)|
1,34,20,19,0,0,0,0,0,0,0,-1--1--1,,1|(202,391)|
1,35,21,19,0,0,0,0,0,0,0,-1--1--1,,1|(198,426)|
1,36,17,16,0,0,0,0,0,0,0,-1--1--1,,1|(473,389)|
1,37,18,16,0,0,0,0,0,0,0,-1--1--1,,1|(474,426)|
10,38,Initial Population,132,491,40,20,8,3,0,0,-1,0,0,0
///---\\\
:GRAPH POPULATION_AND_CARRYING_
:TITLE Population and Carrying Capacity
:X-AXIS time
:SCALE
:VAR Population
:VAR Carrying Capacity
:GRAPH NET_BIRTHS
:TITLE Net Births
:SCALE
:VAR Birth Rate
:VAR Death Rate
:VAR Net birth rate
:GRAPH FRACTIONAL_RATES
:TITLE Fractional Rates
:X-AXIS time
:SCALE
:VAR Fractional Birth Rate
:VAR Fractional Death Rate
:VAR Net Fractional Birth Rate
:GRAPH PHASE_PLOT
:TITLE Phase Plot
:X-AXIS Population Relative to Carrying Capacity
:SCALE
:VAR Birth Rate
:VAR Death Rate
:VAR Net Birth Rate
:GRAPH FRACTIONAL_PHASE_PLOT
:TITLE Fractional Phase Plot
:X-AXIS Population Relative to Carrying Capacity
:SCALE
:VAR Fractional Birth Rate
:VAR Fractional Death Rate
:VAR Net Fractional Birth Rate
:L<%^E!@
9:Current
15:0,0,0,0
19:100,0
5:Net Fractional Birth Rate
